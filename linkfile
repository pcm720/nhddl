/*
# _____     ___ ____     ___ ____
#  ____|   |    ____|   |        | |____|
# |     ___|   |____ ___|    ____| |    \    PS2DEV Open Source Project.
#-----------------------------------------------------------------------
# Copyright 2001-2004, ps2dev - http://www.ps2dev.org
# Licenced under Academic Free License version 2.0
# Review ps2sdk README & LICENSE files for further details.
#
# Linkfile script for ee-ld
*/

ENTRY(__start);

MEMORY {
	bram	: ORIGIN = 0x000f8000, LENGTH = 0x8000 /* 0x000ff000 - 0x00100000: Unused kernel memory used to place the ELF loading code */
	gram	: ORIGIN = 0x00100000, LENGTH =    15M /* 0x00100000 - 0x02000000: User memory, limited to 15M to avoid conflicts with the compressed neutrino.elf */
}

REGION_ALIAS("MAIN_REGION", gram);

END_MAIN_REGION = ORIGIN(MAIN_REGION) + LENGTH(MAIN_REGION);

PHDRS {
  text PT_LOAD;
}

SECTIONS {
    # Place libkernel code, data and launch-related functions into the unused kernel memory
	.text : {
		*(._launch_elf) # Custom launch function
		*libkernel.a:*(.text .text.*)
	} >bram
	.rodata : {
		*libkernel.a:*(.rodata .rodata.*)
	} >bram
	.data : {
		*libkernel.a:*(.data .data.*)
		*(._launch_args) # Marks the start of free memory section
	} >bram

	.text : {
		_ftext = . ;
		*(.text)
		*(.text.*)
		*(.gnu.linkonce.t*)
		KEEP(*(.init))
		KEEP(*(.fini))
		QUAD(0)
	} >MAIN_REGION :text

	PROVIDE(_etext = .);
	PROVIDE(etext = .);

	/* Global/static constructors and deconstructors. */
	.ctors ALIGN(16): {
		KEEP(*crtbegin*.o(.ctors))
		KEEP(*(EXCLUDE_FILE(*crtend*.o) .ctors))
		KEEP(*(SORT(.ctors.*)))
		KEEP(*(.ctors))
	} >MAIN_REGION
	.dtors ALIGN(16): {
		KEEP(*crtbegin*.o(.dtors))
		KEEP(*(EXCLUDE_FILE(*crtend*.o) .dtors))
		KEEP(*(SORT(.dtors.*)))
		KEEP(*(.dtors))
	} >MAIN_REGION

	.reginfo : { *(.reginfo) } >MAIN_REGION

	.data ALIGN(128): {
		_fdata = . ;
		*(.data)
		*(.data.*)
		*(.gnu.linkonce.d*)
		SORT(CONSTRUCTORS)
	} >MAIN_REGION

	/* Static data.  */
	.rodata ALIGN(128): {
		*(.rodata)
		*(.rodata.*)
		*(.gnu.linkonce.r*)
	} >MAIN_REGION

	.rdata ALIGN(128): { *(.rdata) } >MAIN_REGION
	.gcc_except_table ALIGN(128): { *(.gcc_except_table) } >MAIN_REGION

	_gp = ALIGN(128) + 0x7ff0;
	.lit4 ALIGN(128): { *(.lit4) } >MAIN_REGION
	.lit8 ALIGN(128): { *(.lit8) } >MAIN_REGION

	.sdata ALIGN(128): {
		*(.sdata)
		*(.sdata.*)
		*(.gnu.linkonce.s*)
	} >MAIN_REGION

	_edata = .;
	PROVIDE(edata = .);

	/* Uninitialized data.  */
	.sbss ALIGN(128) : {
		_fbss = . ;
		*(.sbss)
		*(.sbss.*)
		*(.gnu.linkonce.sb*)
		*(.scommon)
	} >MAIN_REGION

	.bss ALIGN(128) : {
		*(.bss)
		*(.bss.*)
		*(.gnu.linkonce.b*)
		*(COMMON)
	} >MAIN_REGION
	_end_bss = .;

	_end = . ;
	PROVIDE(end = .);

	.spad 0x70000000: {
		*(.spad)
	} >MAIN_REGION

	/* Symbols needed by crt0.c.  */
	/* We set a fixed stack size and the pointer for the stack, letting the remaining memory be the heap. */
	PROVIDE(_stack_size = 64 * 1024);
	PROVIDE(_stack = END_MAIN_REGION - _stack_size);
	PROVIDE(_heap_size = -1);
}
